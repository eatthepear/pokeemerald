mapscripts Zone1A_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE1A_POTION)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE1A_QUICK_CLAW)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone1A_EventScript_Exit {
    lockall
    if (var(VAR_ZONE) > 1) {
		clearflag(FLAG_IN_ZONE_NOT_SANCTUARY)
		setvar(VAR_REVISITING_ZONE, 0)
		warpteleport(MAP_ZONE0D, 255, 15, 32)
		waitstate
    } else {
		clearflag(FLAG_IN_ZONE_NOT_SANCTUARY)
        warp(MAP_ZONE1SANCTUARY, 255, 22, 7)
    }
    releaseall
    end
}

script Zone1A_EventScript_LeviathanBattle {
	lock
	faceplayer
	fadeoutbgm(1)
	msgbox(Zone1A_Text_Leviathan1Intro)
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	if (flag(FLAG_BRUTAL_MODE_ON) == true) {
    	setvar(VAR_MUGSHOT_ID, MUGSHOT_COREY)
		trainerbattle_no_intro(TRAINER_COREY_LEVIATHAN_1_BRUTAL, Zone1A_Text_Leviathan1Defeat)
	} else {
		setvar(VAR_MUGSHOT_ID, MUGSHOT_COREY)
		trainerbattle_no_intro(TRAINER_COREY_LEVIATHAN_1, Zone1A_Text_Leviathan1Defeat)
	}
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	msgbox(format("{COLOR BLUE}Corey{COLOR DARK_GRAY}: I thought if I gave my Skwovet an Oran Berry, I would be able to win. Oran Berries really are the best!\p Here, let me share some with you and your Pokémon!"))
	finditem(ITEM_ORAN_BERRY, 10)
	msgbox(format("{COLOR BLUE}Corey{COLOR DARK_GRAY}: Well, anyways, I've gotta run now.\pBye bye!"))
	closemessage
	fadescreen(FADE_TO_BLACK)
	removeobject(6)
	setflag(FLAG_BEAT_LEVIATHAN_1)
	fadescreen(FADE_FROM_BLACK)
	release
	end
}

text Zone1A_Text_Leviathan1Intro {
	format("{COLOR BLUE}Corey{COLOR DARK_GRAY}: Hi! My name's Corey.\n"
		   "I'm the resident Leviathan around here.\p"
		   "If you want to go forward, you'll have to beat me!\p"
		   "So, let's get right to it!\p"
		   "I, {COLOR BLUE}Leviathan Corey{COLOR DARK_GRAY}, challenge you to a battle!")
}

text Zone1A_Text_Leviathan1Defeat {
	format("Drats! Even the Oran Berry wasn't enough!")
}

script Zone1A_EventScript_ItemPotion {
	lock
	if (flag(FLAG_ITEM_ZONE1A_POTION)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE1A_POTION)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_POTION, 1)
	release
	end
}

script Zone1A_EventScript_ItemQuickClaw {
	lock
	if (flag(FLAG_ITEM_ZONE1A_QUICK_CLAW)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE1A_QUICK_CLAW)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_QUICK_CLAW, 1)
	release
	end
}

raw `
Zone1A_EventScript_Nina::
	trainerbattle_single TRAINER_NINA, Zone1A_Text_NinaIntro, Zone1A_Text_NinaDefeat
	msgbox Zone1A_Text_NinaPostBattle, MSGBOX_AUTOCLOSE
	end

Zone1A_Text_NinaIntro:
	.string "Hey, you! Let's battle!$"

Zone1A_Text_NinaDefeat:
	.string "Aww… Come on!$"

Zone1A_Text_NinaPostBattle:
	.string "I always lose…$"

Zone1A_EventScript_Mariam::
	trainerbattle_single TRAINER_MARIAM, Zone1A_Text_MariamIntro, Zone1A_Text_MariamDefeat
	msgbox Zone1A_Text_MariamPostBattle, MSGBOX_AUTOCLOSE
	end

Zone1A_Text_MariamIntro:
	.string "Be careful, I'm tough!$"

Zone1A_Text_MariamDefeat:
	.string "Sheesh!$"

Zone1A_Text_MariamPostBattle:
	.string "Maybe I need more Pokémon.$"

Zone1A_EventScript_Oscar::
	trainerbattle_single TRAINER_OSCAR, Zone1A_Text_OscarIntro, Zone1A_Text_OscarDefeat
	msgbox Zone1A_Text_OscarPostBattle, MSGBOX_AUTOCLOSE
	end

Zone1A_Text_OscarIntro:
	.string "Fear my might!$"

Zone1A_Text_OscarDefeat:
	.string "Beaten! Disgrace!$"

Zone1A_Text_OscarPostBattle:
	.string "I must reclaim my honor…$"

Zone1A_EventScript_Sign::
	msgbox Zone1A_Text_Sign, MSGBOX_SIGN
	end

Zone1A_Text_Sign:
	.string "Trainer Tips!\p"
	.string "Press {R_BUTTON} to enable\n"
	.string "or disable Auto Run.$"
`
