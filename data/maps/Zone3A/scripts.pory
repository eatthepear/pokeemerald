mapscripts Zone3A_MapScripts {}

script Zone3A_EventScript_Exit {
    lockall
	if (flag(FLAG_BEATLEVIATHAN3) == false) {
		goto(Zone3A_EventScript_LeviathanBattle)
	}
    if (var(VAR_ZONE) > 3) {
	    clearflag(FLAG_IS_REVISITING_ZONE)
	    warpteleport(MAP_ZONE0D, 255, 2, 32)
    } else {
		if ((flag(FLAG_BRUTAL_MODE_ON) == true) &&
			(!defeated(TRAINER_PATRICK) ||
			!defeated(TRAINER_RICHARD) ||
			!defeated(TRAINER_ANNABELLE))) {
			goto(EventScript_BrutalExitNotSatisfied)
		}
        warp(MAP_ZONE3SANCTUARY, 255, 22, 7)
    }
    releaseall
    end
}

script Zone3A_EventScript_LeviathanBattle {
	fadeoutbgm(1)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
	waitmovement(0)
	addobject(10)
	clearflag(FLAG_HIDE_ZONE3A_PHILLIPA)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
	applymovement(10, Common_Movement_WalkDown)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
	waitmovement(0)
	msgbox(Zone3A_Text_Leviathan3Intro)
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	if (flag(FLAG_BRUTAL_MODE_ON) == true) {
    	setvar(VAR_MUGSHOT_ID, MUGSHOT_PHILLIPA)
		trainerbattle_no_intro(TRAINER_PHILLIPA_LEVIATHAN_3, Zone3A_Text_Leviathan3Defeat)
	} else {
		setvar(VAR_MUGSHOT_ID, MUGSHOT_PHILLIPA)
		trainerbattle_no_intro(TRAINER_PHILLIPA_LEVIATHAN_3, Zone3A_Text_Leviathan3Defeat)
	}
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	msgbox(format("{COLOR BLUE}Phillipa{COLOR DARK_GRAY}: Wah!"))
	applymovement(10, Zone3A_Movement_Phillipa_RunAway)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: Phillipa? Are you ok?"))
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	addobject(4)
	clearflag(FLAG_HIDE_ZONE3A_REMI)
	applymovement(4, Common_Movement_WalkDown)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: Oh!"))
	applymovement(4, Common_Movement_FaceLeft)
	waitmovement(0)
	delay(32)
	applymovement(4, Common_Movement_FaceDown)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: I see.\pYou must have defeated my daughter in battle.\pDon't worry, I'll go talk to her."))
	applymovement(4, Zone3A_Movement_Remi_WalkToPhillipa)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: Hey, kiddo."))
	applymovement(10, Common_Movement_FaceDown)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Phillipa{COLOR DARK_GRAY}: Wah! Daddy! I lost!"))
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: I know, love, but sometimes we lose, and that's life. We have to face our losses with honor, right?"))
	delay(32)
	msgbox(format("{COLOR BLUE}Phillipa{COLOR DARK_GRAY}: Sniff…\pOkay…"))
	applymovement(4, Zone3A_Movement_Remi_WalkBack)
	applymovement(10, Zone3A_Movement_Phillipa_WalkBack)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: What do you say?"))
	msgbox(format("{COLOR BLUE}Phillipa{COLOR DARK_GRAY}: Sniff… Thank you very much for the battle!"))
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: I'm proud of you, kiddo."))
	applymovement(4, Zone3A_Movement_Remi_WalkToPlayer)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: Thanks for being a good sport.\pHere, I want you to have this."))
	giveitem(ITEM_GOOD_ROD, 1)
	applymovement(4, Zone3A_Movement_Remi_WalkBackToPhillipa)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: Alright, that's enough excitement for one day.\pIt's dinner time."))
	applymovement(10, Common_Movement_FaceRight)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Phillipa{COLOR DARK_GRAY}: Food!"))
	closemessage
	applymovement(10, Common_Movement_WalkUp)
	waitmovement(0)
	setflag(FLAG_HIDE_ZONE3A_PHILLIPA)
	removeobject(10)
	applymovement(4, Common_Movement_WalkLeft)
	waitmovement(0)
	applymovement(4, Common_Movement_WalkUp)
	waitmovement(0)
	setflag(FLAG_HIDE_ZONE3A_REMI)
	removeobject(4)
	setflag(FLAG_BEATLEVIATHAN3)
	release
	end
}

text Zone3A_Text_Leviathan3Intro {
	format("{COLOR BLUE}Phillipa{COLOR DARK_GRAY}: You! How dare you intrude on my home!\p"
	"I am {COLOR BLUE}Leviathan Phillipa{COLOR DARK_GRAY}, all powerful!")
}

text Zone3A_Text_Leviathan3Defeat {
	format("Wah!!!")
}

movement Zone3A_Movement_Phillipa_RunAway {
	walk_fast_left * 3
	walk_fast_up * 3
}

movement Zone3A_Movement_Remi_WalkToPhillipa {
	walk_left * 3
	walk_up * 2
}

movement Zone3A_Movement_Remi_WalkBack {
	walk_down * 2
	walk_right * 4
	face_down
}

movement Zone3A_Movement_Phillipa_WalkBack {
	walk_down * 3
	walk_right * 3
	face_down
}

movement Zone3A_Movement_Remi_WalkToPlayer {
	walk_down
	face_left
}

movement Zone3A_Movement_Remi_WalkBackToPhillipa {
	walk_up
	face_left
}

raw `

Zone3A_EventScript_Patrick::
	trainerbattle_single TRAINER_PATRICK, Zone3A_Text_PatrickIntro, Zone3A_Text_PatrickDefeat
	msgbox Zone3A_Text_PatrickPostBattle, MSGBOX_AUTOCLOSE
	end

Zone3A_Text_PatrickIntro:
	.string "My friends call me the free Exp guy,\n"
	.string "but I'll prove them wrong!$"

Zone3A_Text_PatrickDefeat:
	.string "Oops, guess not...$"

Zone3A_Text_PatrickPostBattle:
	.string "I guess I am the free Exp guy...$"

Zone3A_EventScript_Jemma::
	trainerbattle_single TRAINER_JEMMA, Zone3A_Text_JemmaIntro, Zone3A_Text_JemmaDefeat
	msgbox Zone3A_Text_JemmaPostBattle, MSGBOX_AUTOCLOSE
	end

Zone3A_Text_JemmaIntro:
	.string "Hey! You have to pay to cross!$"

Zone3A_Text_JemmaDefeat:
	.string "Uh... You can go for free!$"

Zone3A_Text_JemmaPostBattle:
	.string "I was scamming people so well too...$"

Zone3A_EventScript_Boris::
	trainerbattle_single TRAINER_BORIS, Zone3A_Text_BorisIntro, Zone3A_Text_BorisDefeat
	msgbox Zone3A_Text_BorisPostBattle, MSGBOX_AUTOCLOSE
	end

Zone3A_Text_BorisIntro:
	.string "Never give up, never surrender!$"

Zone3A_Text_BorisDefeat:
	.string "I surrender!$"

Zone3A_Text_BorisPostBattle:
	.string "I'm a hypocrite, I know.$"

Zone3A_EventScript_Annabelle::
	trainerbattle_single TRAINER_ANNABELLE, Zone3A_Text_AnnabelleIntro, Zone3A_Text_AnnabelleDefeat
	msgbox Zone3A_Text_AnnabellePostBattle, MSGBOX_AUTOCLOSE
	end

Zone3A_Text_AnnabelleIntro:
	.string "I wonder what day it is...$"

Zone3A_Text_AnnabelleDefeat:
	.string "Apparently it's defeat day!$"

Zone3A_Text_AnnabellePostBattle:
	.string "Tomorrow will be another day.$"

Zone3A_EventScript_Richard::
	trainerbattle_single TRAINER_RICHARD, Zone3A_Text_RichardIntro, Zone3A_Text_RichardDefeat
	msgbox Zone3A_Text_RichardPostBattle, MSGBOX_AUTOCLOSE
	end

Zone3A_Text_RichardIntro:
	.string "I have far too much money to deal\n"
	.string "with the likes of you!$"

Zone3A_Text_RichardDefeat:
	.string "Agh! My money!$"

Zone3A_Text_RichardPostBattle:
	.string "Do you think I can buy humility\n"
	.string "with my money?$"

Zone3A_EventScript_ItemTM03::
	finditem ITEM_TM03
	end

Zone3A_EventScript_ItemPotion::
	finditem ITEM_POTION
	end

Zone3A_EventScript_ItemParalyzeHeal::
	finditem ITEM_PARALYZE_HEAL
	end

Zone3A_EventScript_ItemHyperPotion::
	finditem ITEM_HYPER_POTION
	end
