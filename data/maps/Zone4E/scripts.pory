mapscripts Zone4E_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE4E_SUPER_POTION)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone4E_EventScript_LeviathanBattleLeft {
	lockall
	applymovement(1, Common_Movement_WalkDown)
	waitmovement(0)
	applymovement(1, Common_Movement_WalkDown)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkRight)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
	waitmovement(0)
	goto(Zone4E_EventScript_LeviathanBattle)
}

script Zone4E_EventScript_LeviathanBattleMiddle {
	lockall
	applymovement(1, Common_Movement_WalkDown)
	waitmovement(0)
	applymovement(1, Common_Movement_WalkDown)
	waitmovement(0)
	goto(Zone4E_EventScript_LeviathanBattle)
}

script Zone4E_EventScript_LeviathanBattleRight {
	lockall
	applymovement(1, Common_Movement_WalkDown)
	waitmovement(0)
	applymovement(1, Common_Movement_WalkDown)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
	waitmovement(0)
	goto(Zone4E_EventScript_LeviathanBattle)
}

script Zone4E_EventScript_LeviathanBattle {
	fadeoutbgm(1)
	msgbox(Zone4E_Text_Leviathan4Intro)
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	if ((flag(FLAG_SETTINGS_BRUTAL) == true) || (flag(FLAG_SETTINGS_CHALLENGE) == true)) {
    	setvar(VAR_MUGSHOT_ID, MUGSHOT_RITA)
		trainerbattle_no_intro(TRAINER_RITA_LEVIATHAN_4_BRUTAL, Zone4E_Text_Leviathan4Defeat)
	} else {
		setvar(VAR_MUGSHOT_ID, MUGSHOT_RITA)
		trainerbattle_no_intro(TRAINER_RITA_LEVIATHAN_4, Zone4E_Text_Leviathan4Defeat)
	}
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	msgbox(format("{COLOR BLUE}Rita{COLOR DARK_GRAY}: That was great! I hope our Pokémon are friends now, too!\pHere, I want you to have this gift, from me!\pThe best way to bring out one's inner power is through friendship!"))
	giveitem(ITEM_TM02)
	msgbox(format("{COLOR BLUE}Rita{COLOR DARK_GRAY}: Alright, I'm off to go meet some more friends!\pSee you around!"))
	closemessage
	fadescreenswapbuffers(FADE_TO_BLACK)
	removeobject(1)
	setflag(FLAG_BEAT_LEVIATHAN_4)
	setvar(VAR_ZONE_4_STATE, 1)
	fadescreenswapbuffers(FADE_FROM_BLACK)
	if (flag(FLAG_SETTINGS_BRUTAL) == true) {
		setflag(FLAG_ZONE4A_RECEIVED_GEM)
	}
	releaseall
	end
}

text Zone4E_Text_Leviathan4Intro {
	format("{COLOR BLUE}Rita{COLOR DARK_GRAY}: Hey! I'm {COLOR BLUE}Rita{COLOR DARK_GRAY}.\pNice to meet you!\pPeople always criticize me for being too friendly with others.\pBut I just find that's the best way to make new friends!\pWell, how about a battle, friend?")
}

text Zone4E_Text_Leviathan4Defeat {
	"That was a lovely battle.\n"
	"Thank you!"
}

script Zone4E_EventScript_ItemSuperPotion {
	lock
	if (flag(FLAG_ITEM_ZONE4E_SUPER_POTION)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE4E_SUPER_POTION)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_SUPER_POTION, 1)
	release
	end
}

raw `
Zone4E_EventScript_Sign::
	msgbox Zone4E_Text_Sign, MSGBOX_SIGN
	end

Zone4E_Text_Sign:
	.string "Trainer Tips!\p"
	.string "Golden Trees often have Berries\n"
	.string "or wild Pokémon!$"
`
