mapscripts Zone18A_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE18A_SUPER_POTION_2)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE18A_SUPER_REPEL)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone18A_EventScript_PirateEvent {
	lockall
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Eli{COLOR DARK_GRAY}: There it is! My Azurill doll!"))
	special(SpawnCameraObject)
	applymovement(OBJ_EVENT_ID_CAMERA, Zone18A_Movement_CameraDown)
	waitmovement(0)
	playbgm(MUS_ENCOUNTER_AQUA, 0)
	addobject(7)
	addobject(8)
	clearflag(FLAG_HIDE_ZONE18A_PIRATES)
	delay(40)
	applymovement(7, Zone18A_Movement_Pirate1_Left)
	applymovement(8, Zone18A_Movement_Pirate2_Left)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Zoe{COLOR DARK_GRAY}: What the?"), MSGBOX_DEFAULT)
	msgbox(format("{COLOR BLUE}Eli{COLOR DARK_GRAY}: Pirates!"), MSGBOX_DEFAULT)
	msgbox(format("{COLOR BLUE}Eli{COLOR DARK_GRAY}: Hey, wait! That's my special doll! Don't take it, please!"))
	applymovement(7, Common_Movement_FaceUp)
	waitmovement(0)
	msgbox(format("It be ours now, laddy!"))
	applymovement(7, Common_Movement_FaceDown)
	waitmovement(0)
	removeobject(6)
	setflag(FLAG_HIDE_ZONE18A_DOLL)
	delay(40)
	applymovement(7, Zone18A_Movement_Pirate1_Right)
	applymovement(8, Zone18A_Movement_Pirate2_Right)
	waitmovement(0)
	removeobject(7)
	removeobject(8)
	setflag(FLAG_HIDE_ZONE18A_PIRATES)
	applymovement(OBJ_EVENT_ID_CAMERA, Zone18A_Movement_CameraUp)
	waitmovement(0)
	special(RemoveCameraObject)
	removeobject(11)
	setflag(FLAG_HIDE_ZONE18A_BOAT)
	applymovement(9, Common_Movement_FaceRight)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Eli{COLOR DARK_GRAY}: Wah!!!"), MSGBOX_DEFAULT)
	applymovement(10, Common_Movement_FaceLeft)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Zoe{COLOR DARK_GRAY}: It'll be ok, Eli!"), MSGBOX_DEFAULT)
	applymovement(10, Common_Movement_FaceUp)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Zoe{COLOR DARK_GRAY}: {PLAYER}! You're stronger than both of us.\pCan you chase the pirates and recover the doll?\pHere, you can borrow this Diving Gear; it might be helpful."), MSGBOX_DEFAULT)
	giveitem(ITEM_DIVING_GEAR)
	applymovement(9, Common_Movement_FaceUp)
	waitmovement(0)
	msgbox(Zone18A_Text_Eli, MSGBOX_DEFAULT)
	fadedefaultbgm
	setflag(FLAG_HIDE_ZONE19D_PIRATES)
	setvar(VAR_ZONE_18_STATE, 4)
	release
	end
}

movement Zone18A_Movement_CameraDown {
	walk_down * 5
}

movement Zone18A_Movement_Pirate1_Left {
	walk_left * 6
	face_down
}

movement Zone18A_Movement_Pirate2_Left {
	walk_left * 5
}

movement Zone18A_Movement_Pirate1_Right {
	walk_right * 6
}

movement Zone18A_Movement_Pirate2_Right {
	walk_right * 5
}

movement Zone18A_Movement_CameraUp {
	walk_up * 5
}

text Zone18A_Text_Eli {
	"{COLOR BLUE}Eli{COLOR DARK_GRAY}: Please! Help!"
}

text Zone18A_Text_Zoe {
	format("{COLOR BLUE}Zoe{COLOR DARK_GRAY}: Please, recover my brother's Azurill doll from the pirates.")
}

script Zone18A_EventScript_Eli {
	lock
	faceplayer
	msgbox(Zone18A_Text_Eli)
	release
	end
}

script Zone18A_EventScript_Zoe {
	lock
	faceplayer
	msgbox(Zone18A_Text_Zoe)
	release
	end
}

script Zone18A_EventScript_AzurillDoll {
	msgbox("It's an Azurill doll.\nIt's clearly been well loved.", MSGBOX_SIGN)
	end
}

script Zone18A_EventScript_SellSodaPop {
	lock
	faceplayer
	if ((flag(FLAG_SETTINGS_BRUTAL) == true) || (flag(FLAG_SETTINGS_INDEPENDENT) == true)) {
		msgbox(format("Soda Pop! I usually sell it, but today I'm out of stock!"))
		release
		end
	}
	showmoneybox(0, 0, 0)
	msgbox("Want to buy some Soda Pop?\nÂ¥300 for 50 HP a pop!", MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		checkmoney(300, 0)
		if (var(VAR_RESULT) == FALSE) {
			msgbox("You don't have enough money!")
			hidemoneybox
		} else {
			msgbox("Here you go!")
			removemoney(300, 0)
			updatemoneybox
			giveitem(ITEM_SODA_POP)
			hidemoneybox
		}
	} else {
		hidemoneybox
		msgbox("Aww, did you not like my pun?")
	}
	release
	end
}

script Zone18A_EventScript_SuperPotion2 {
	lock
	if (flag(FLAG_ITEM_ZONE18A_SUPER_POTION_2)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE18A_SUPER_POTION_2)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_SUPER_POTION, 2)
	release
	end
}

script Zone18A_EventScript_SuperRepel {
	lock
	if (flag(FLAG_ITEM_ZONE18A_SUPER_REPEL)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE18A_SUPER_REPEL)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_SUPER_REPEL, 1)
	release
	end
}

raw `
Zone18A_EventScript_Sign::
	msgbox Zone18A_Text_Sign, MSGBOX_SIGN
	end

Zone18A_Text_Sign:
	.string "Trainer's Dojo!\n"
	.string "All challengers welcome!$"

Zone18A_EventScript_Doris::
	trainerbattle_single TRAINER_DORIS, Zone18A_Text_DorisIntro, Zone18A_Text_DorisDefeat
	msgbox Zone18A_Text_DorisPostBattle, MSGBOX_AUTOCLOSE
	end

Zone18A_Text_DorisIntro:
	.string "I've wanted a relaxing vacation for\n"
	.string "so long, but it's been so boring!\p"
	.string "How about a battle to spice things up?$"

Zone18A_Text_DorisDefeat:
	.string "Now that was exciting!$"

Zone18A_Text_DorisPostBattle:
	.string "I'll check out the Trainer's Dojo.\n"
	.string "It'll be more fun than just\l"
	.string "lounging around, right?$"

Zone18A_EventScript_Homer::
	trainerbattle_single TRAINER_HOMER, Zone18A_Text_HomerIntro, Zone18A_Text_HomerDefeat
	msgbox Zone18A_Text_HomerPostBattle, MSGBOX_AUTOCLOSE
	end

Zone18A_Text_HomerIntro:
	.string "Ah, a lovely day by the beach.\p"
	.string "What's that? A battle?\p"
	.string "Say no more! I'm all riled up!$"

Zone18A_Text_HomerDefeat:
	.string "Argh! You ruined my perfect day!$"

Zone18A_Text_HomerPostBattle:
	.string "My day is ruined! How am I supposed\n"
	.string "to enjoy my day at the beach\l"
	.string "knowing I've lost to some commoner?$"
`
