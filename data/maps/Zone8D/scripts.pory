mapscripts Zone8D_MapScripts {
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_ZONE_8_STATE, 0: Zone8D_EventScript_WallyIntro
	]
}

script Zone8D_EventScript_WallyIntro {
	lockall
	applymovement(4, Common_Movement_FaceDown)
	waitmovement(0)
	applymovement(4, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}???{COLOR DARK_GRAY}: Um… E-excuse me…\pI'm {COLOR BLUE}Wally{COLOR DARK_GRAY}…{PAUSE_UNTIL_PRESS} N-nice to meet you…{PAUSE_UNTIL_PRESS} Um…"))
	delay(40)
	msgbox(format("{COLOR BLUE}Wally{COLOR DARK_GRAY}: Are you taking on the {COLOR RED}Pokémon Battle Labyrinth{COLOR DARK_GRAY} too?\lThese two Zones have b-been quite a challenge for me so far.\pI'm s-sure the exit is just a little ahead."))
	applymovement(4, Common_Movement_FaceUp)
	waitmovement(0)
	special(SpawnCameraObject)
	applymovement(OBJ_EVENT_ID_CAMERA, Zone8D_Movement_Camera_GoUp)
	waitmovement(0)
	special(RemoveCameraObject)
	msgbox(format("{COLOR BLUE}Wally{COLOR DARK_GRAY}: I need to pass through, b-but they've been blocking the way.\pThey look really strong though…\nI'm worried I w-won't be able to defeat them alone."))
	setvar(VAR_ZONE_8_STATE, 1)
	special(SpawnCameraObject)
	applymovement(OBJ_EVENT_ID_CAMERA, Zone8D_Movement_Camera_GoDown)
	waitmovement(0)
	special(RemoveCameraObject)
	applymovement(4, Common_Movement_FaceDown)
	waitmovement(0)
	msgbox(Zone8D_Text_Wally_YesNo, MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		goto(Zone8D_EventScript_InitiateLeviathan8)
	}
	msgbox(Zone8D_Text_Wally_Preparations)
	releaseall
	end
}

script Zone8D_EventScript_WallyYesNo {
	lock
	faceplayer
	msgbox(Zone8D_Text_Wally_YesNo, MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		goto(Zone8D_EventScript_InitiateLeviathan8)
	}
	msgbox(Zone8D_Text_Wally_Preparations)
	release
	end

}

text Zone8D_Text_Wally_YesNo {
	format("{COLOR BLUE}Wally{COLOR DARK_GRAY}: Will you join forces with me to defeat them?")
}

text Zone8D_Text_Wally_Preparations {
	format("You… must have preparations to do.\pI-I'll just be waiting here, for you. Let me know when you're ready.")
}

script Zone8D_EventScript_GoWithWally {
	lockall
	msgbox(format("You don't want to battle without {COLOR BLUE}Wally{COLOR DARK_GRAY}. His aid will be invaluable."))
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
	waitmovement(0)
	releaseall
	end
}

script Zone8D_EventScript_InitiateLeviathan8 {
	msgbox(format("Thank you. What's your name?"))
	delay(48)
	msgbox(format("{COLOR BLUE}{PLAYER}{COLOR DARK_GRAY}. I'll remember that."))
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	if (var(VAR_TEMP_0) == 6) {
		applymovement(4, Common_Movement_WalkUp)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkRight)
		waitmovement(0)
		applymovement(4, Common_Movement_WalkUp)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
		waitmovement(0)
	} elif (var(VAR_TEMP_0) == 7) {
		if (var(VAR_TEMP_1) == 11) {
			applymovement(4, Common_Movement_WalkUp)
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
			waitmovement(0)
			applymovement(4, Common_Movement_WalkUp)
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
			waitmovement(0)
		} else {
			applymovement(4, Common_Movement_WalkUp)
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkRight)
			waitmovement(0)
			applymovement(4, Common_Movement_WalkUp)
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
			waitmovement(0)
		}
	} else {
		applymovement(4, Common_Movement_WalkUp)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
		waitmovement(0)
		applymovement(4, Common_Movement_WalkUp)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
		waitmovement(0)
	}
	applymovement(4, Common_Movement_WalkUp)
	applymovement(OBJ_EVENT_ID_PLAYER, Zone8D_Movement_Player_WalkUp)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Wally{COLOR DARK_GRAY}: E-excuse me! We would like to pass through!"))
	applymovement(2, Common_Movement_ExclamationMark)
	applymovement(3, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	fadeoutbgm(1)
	applymovement(2, Common_Movement_FaceDown)
	applymovement(3, Common_Movement_FaceDown)
	waitmovement(0)
	delay(96)
	applymovement(2, Common_Movement_FaceRight)
	applymovement(3, Common_Movement_FaceLeft)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}???{COLOR DARK_GRAY}: It is back again.\nWith a friend this time."))
	msgbox(format("{COLOR BLUE}???{COLOR DARK_GRAY}: This should be… entertaining.\pDeal with them."))
	applymovement(3, Common_Movement_FaceDown)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}???{COLOR DARK_GRAY}: At once."))
	applymovement(2, Common_Movement_FaceUp)
	waitmovement(0)
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	while {
		msgbox(gText_PleasePick3Mons)
		choose_mons
		if (var(VAR_RESULT) != 0) {
			break
		}
	}
	setvar(VAR_MUGSHOT_ID, MUGSHOT_MIRAGE)
	if ((flag(FLAG_SETTINGS_BRUTAL) == true) || (flag(FLAG_SETTINGS_CHALLENGE) == true)) {
		multi_2_vs_1(TRAINER_MIRAGE_LEVIATHAN_8_BRUTAL, "…", TRAINER_WALLY_ZONE8D, TRAINER_BACK_PIC_WALLY)
	} else {
		multi_2_vs_1(TRAINER_MIRAGE_LEVIATHAN_8, "…", TRAINER_WALLY_ZONE8D, TRAINER_BACK_PIC_WALLY)
	}
	specialvar(VAR_RESULT, GetBattleOutcome)
	if (var(VAR_RESULT) != B_OUTCOME_WON) {
		fadescreenswapbuffers(FADE_TO_BLACK)
		special(SetCB2WhiteOut)
		waitstate
	}
	applymovement(3, Common_Movement_FaceLeft)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}???{COLOR DARK_GRAY}: I was defeated…"))
	applymovement(2, Common_Movement_FaceRight)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}???{COLOR DARK_GRAY}: No matter. It's done."))
	fadescreenswapbuffers(FADE_TO_BLACK)
	playmoncry(SPECIES_PORYGON, 2)
	delay(40)
	waitmoncry
	removeobject(1)
	removeobject(2)
	removeobject(3)
	setflag(FLAG_BEAT_LEVIATHAN_8)
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	fadescreenswapbuffers(FADE_FROM_BLACK)
	msgbox(format("{COLOR BLUE}Wally{COLOR DARK_GRAY}: Who were those p-people?\nAnd what was with that Pokémon they had with them?\pThat was a Shiny {COLOR BLUE}Porygon{COLOR DARK_GRAY}, w-wasn't it?"))
	delay(32)
	applymovement(4, Common_Movement_FaceRight)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Wally{COLOR DARK_GRAY}: Um… anyways, thanks for your help, {PLAYER}.\pI um… I want you to have this."))
	giveitem(ITEM_EVIOLITE, 1)
	msgbox(format("{COLOR BLUE}Wally{COLOR DARK_GRAY}: It's not… the flashiest item, but I'm sure it'll be useful."))
	applymovement(4, Common_Movement_WalkUp)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
	waitmovement(0)
	special(SpawnCameraObject)
	applymovement(OBJ_EVENT_ID_CAMERA, Zone8D_Movement_Camera_GoUp)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
	applymovement(4, Zone8D_Movement_Wally_WalkUp)
	waitmovement(0)
	special(RemoveCameraObject)
	applymovement(4, Common_Movement_FaceDown)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Wally{COLOR DARK_GRAY}: I hope we meet again.\pGoodbye, {PLAYER}."))
	delay(40)
	applymovement(4, Common_Movement_FaceUp)
	waitmovement(0)
	fadescreenswapbuffers(FADE_TO_BLACK)
	playmoncry(SPECIES_PORYGON, 0)
	waitmoncry
	setflag(FLAG_HIDE_ZONE8D_WALLY)
	removeobject(4)
	fadescreenswapbuffers(FADE_FROM_BLACK)
	special(SpawnCameraObject)
	applymovement(OBJ_EVENT_ID_CAMERA, Zone8D_Movement_Camera_GoDown)
	waitmovement(0)
	special(RemoveCameraObject)
	setvar(VAR_ZONE_8_STATE, 2)
	releaseall
	end
}

movement Zone8D_Movement_Camera_GoUp {
	walk_up * 3
}

movement Zone8D_Movement_Camera_GoDown {
	walk_down * 3
}

movement Zone8D_Movement_Wally_WalkUp {
	walk_up * 3
}

movement Zone8D_Movement_Player_WalkUp {
	walk_up
	walk_right
	walk_up
}
