mapscripts Zone18A_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE18A_SUPER_POTION_2)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE18A_SUPER_REPEL)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone18A_EventScript_LeviathanBattle {
	lockall
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Zoe{COLOR DARK_GRAY}: Is this the challenger you were telling me about?"), MSGBOX_DEFAULT)
	msgbox(format("{COLOR BLUE}Eli{COLOR DARK_GRAY}: Yeah yeah!"), MSGBOX_DEFAULT)
	fadeoutbgm(1)
	msgbox(format("{COLOR BLUE}Zoe{COLOR DARK_GRAY}: To truly clear the Trainer's Dojo, you must defeat the two of us in a Double Battle!"), MSGBOX_DEFAULT)
	msgbox(format("{COLOR BLUE}Eli{COLOR DARK_GRAY}: Taste our power!"), MSGBOX_DEFAULT)
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	if (flag(FLAG_BRUTAL_MODE_ON) == true) {
		setweather(WEATHER_RAIN)
		doweather
		msgbox("It suddenly began to rain!", MSGBOX_DEFAULT)
    	setvar(VAR_MUGSHOT_ID, MUGSHOT_LILY)
		trainerbattle_no_intro(TRAINER_ZOE_AND_ELI_LEVIATHAN_16_BRUTAL, Zone18A_Text_Leviathan16Defeat)
		resetweather
		doweather
	} else {
		setvar(VAR_MUGSHOT_ID, MUGSHOT_LILY)
		trainerbattle_no_intro(TRAINER_ZOE_AND_ELI_LEVIATHAN_16, Zone18A_Text_Leviathan16Defeat)
	}
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	msgbox(format("{COLOR BLUE}Eli{COLOR DARK_GRAY}: Aww! We lost, sis!"), MSGBOX_DEFAULT)
	msgbox(format("{COLOR BLUE}Zoe{COLOR DARK_GRAY}: That's ok, Eli. We'll learn from this, yeah?"), MSGBOX_DEFAULT)
	msgbox(format("{COLOR BLUE}Eli{COLOR DARK_GRAY}: Yeah yeah! And we gotta give {PLAYER} the reward!"), MSGBOX_DEFAULT)
	msgbox(format("{COLOR BLUE}Zoe{COLOR DARK_GRAY}: Right! Here's our little gift for clearing the Trainer's Dojo."), MSGBOX_DEFAULT)
	giveitem(ITEM_TM36)
	playbgm(MUS_ENCOUNTER_AQUA, 0)
	clearflag(FLAG_HIDE_ZONE18A_PIRATES)
	applymovement(9, Zone18A_Movement_Pirate1_Left)
	applymovement(10, Zone18A_Movement_Pirate2_Left)
	msgbox(format("{COLOR BLUE}Zoe{COLOR DARK_GRAY}: What the?"), MSGBOX_DEFAULT)
	msgbox(format("{COLOR BLUE}Eli{COLOR DARK_GRAY}: Pirates!"), MSGBOX_DEFAULT)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Eli{COLOR DARK_GRAY}: Hey, wait! That's my special doll! Don't take it, please!"), MSGBOX_DEFAULT)
	msgbox(format("It be ours now, laddy!"), MSGBOX_DEFAULT)
	removeobject(8)
	setflag(FLAG_HIDE_ZONE18A_DOLL)
	applymovement(9, Zone18A_Movement_Pirate1_Right)
	applymovement(10, Zone18A_Movement_Pirate2_Right)
	waitmovement(0)
	removeobject(9)
	removeobject(10)
	setflag(FLAG_HIDE_ZONE18A_PIRATES)
	msgbox(format("{COLOR BLUE}Eli{COLOR DARK_GRAY}: Wah!!!"), MSGBOX_DEFAULT)
	msgbox(format("{COLOR BLUE}Zoe{COLOR DARK_GRAY}: It'll be ok, Eli!"), MSGBOX_DEFAULT)
	msgbox(format("{COLOR BLUE}Zoe{COLOR DARK_GRAY}: {PLAYER}! You're stronger than both of us.\pCan you chase the pirates and recover the doll?\pHere, this Diving Gear might be helpful."), MSGBOX_DEFAULT)
	giveitem(ITEM_DIVING_GEAR)
	fadedefaultbgm
	msgbox(format("{COLOR BLUE}Eli{COLOR DARK_GRAY}: Please! Help!"), MSGBOX_DEFAULT)
	setflag(FLAG_BEAT_LEVIATHAN_16)
	setvar(VAR_ZONE_18_STATE, 1)
	release
	end
}

text Zone18A_Text_Leviathan16Defeat {
	format("{COLOR BLUE}Eli{COLOR DARK_GRAY}: Darn it!\p{COLOR BLUE}Zoe{COLOR DARK_GRAY}: It seems you've defeated us.")
}

movement Zone18A_Movement_Pirate1_Left {
	walk_faster_left * 7
	walk_faster_down * 3
}

movement Zone18A_Movement_Pirate2_Left {
	walk_faster_left * 4
	walk_faster_down * 3
	walk_faster_left * 2
}

movement Zone18A_Movement_Pirate1_Right {
	walk_faster_up * 3
	walk_faster_right * 7
}

movement Zone18A_Movement_Pirate2_Right {
	walk_faster_right * 2
	walk_faster_up * 3
	walk_faster_right * 4
}

script Zone18A_EventScript_AzurillDoll {
	msgbox("It's an Azurill doll.\nIt's clearly been well loved.", MSGBOX_SIGN)
	end
}

script Zone18A_EventScript_SellSodaPop {
	lock
	faceplayer
	showmoneybox(0, 0, 0)
	msgbox("Want to buy some Soda Pop?\nÂ¥300 for 50 HP a pop!", MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		checkmoney(300, 0)
		if (var(VAR_RESULT) == FALSE) {
			msgbox("You don't have enough money!")
			hidemoneybox
		} else {
			msgbox("Here you go!")
			removemoney(300, 0)
			updatemoneybox
			giveitem(ITEM_SODA_POP)
			hidemoneybox
		}
	} else {
		hidemoneybox
		msgbox("Aww, did you not like my pun?")
	}
	release
	end
}

script Zone18A_EventScript_SuperPotion2 {
	lock
	if (flag(FLAG_ITEM_ZONE18A_SUPER_POTION_2)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE18A_SUPER_POTION_2)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_SUPER_POTION, 2)
	release
	end
}

script Zone18A_EventScript_SuperRepel {
	lock
	if (flag(FLAG_ITEM_ZONE18A_SUPER_REPEL)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE18A_SUPER_REPEL)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_SUPER_REPEL, 1)
	release
	end
}

raw `
Zone18A_EventScript_Sign::
	msgbox Zone18A_Text_Sign, MSGBOX_SIGN
	end

Zone18A_Text_Sign:
	.string "Trainer's Dojo!\n"
	.string "All challengers welcome!$"

Zone18A_EventScript_Doris::
	trainerbattle_single TRAINER_DORIS, Zone18A_Text_DorisIntro, Zone18A_Text_DorisDefeat
	msgbox Zone18A_Text_DorisPostBattle, MSGBOX_AUTOCLOSE
	end

Zone18A_Text_DorisIntro:
	.string "$"

Zone18A_Text_DorisDefeat:
	.string "$"

Zone18A_Text_DorisPostBattle:
	.string "$"

Zone18A_EventScript_Homer::
	trainerbattle_single TRAINER_HOMER, Zone18A_Text_HomerIntro, Zone18A_Text_HomerDefeat
	msgbox Zone18A_Text_HomerPostBattle, MSGBOX_AUTOCLOSE
	end

Zone18A_Text_HomerIntro:
	.string "$"

Zone18A_Text_HomerDefeat:
	.string "$"

Zone18A_Text_HomerPostBattle:
	.string "$"
`
