mapscripts Zone4E_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE4E_SUPER_POTION)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone4E_EventScript_Exit {
    lockall
    if (var(VAR_ZONE) > 4) {
		call(EventScript_WarpZone0)
    } else {
		specialvar(VAR_NUM_TRAINERS_REMAINING, GetNumTrainersRemaining)
		if ((flag(FLAG_BRUTAL_MODE_ON) == true) && (var(VAR_NUM_TRAINERS_REMAINING) != 0)) {
			goto(EventScript_BrutalExitNotSatisfied)
		}
		setflag(FLAG_RECEIVED_ZONE4A_GEM)
        warp(MAP_ZONE4SANCTUARY, 255, 22, 7)
		waitstate
    }
    releaseall
    end
}

script Zone4E_EventScript_LeviathanBattle {
	lock
	faceplayer
	fadeoutbgm(1)
	msgbox(Zone4E_Text_Leviathan4Intro)
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	if (flag(FLAG_BRUTAL_MODE_ON) == true) {
    	setvar(VAR_MUGSHOT_ID, MUGSHOT_RITA)
		trainerbattle_no_intro(TRAINER_RITA_LEVIATHAN_4_BRUTAL, Zone4E_Text_Leviathan4Defeat)
	} else {
		setvar(VAR_MUGSHOT_ID, MUGSHOT_RITA)
		trainerbattle_no_intro(TRAINER_RITA_LEVIATHAN_4, Zone4E_Text_Leviathan4Defeat)
	}
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	msgbox(format("{COLOR BLUE}Rita{COLOR DARK_GRAY}: That was great! I hope our Pokémon are friends now, too!\pHere, I want you to have this gift, from me!\pThe best way to bring out one's inner power is through friendship!"))
	giveitem(ITEM_TM02)
	msgbox(format("{COLOR BLUE}Rita{COLOR DARK_GRAY}: Alright, I'm off to go meet some more friends!\pSee you around!"))
	closemessage
	fadescreen(FADE_TO_BLACK)
	removeobject(2)
	setflag(FLAG_BEAT_LEVIATHAN_4)
	fadescreen(FADE_FROM_BLACK)
	release
	end
}

text Zone4E_Text_Leviathan4Intro {
	format("{COLOR BLUE}Rita{COLOR DARK_GRAY}: Hey! I'm {COLOR BLUE}Rita{COLOR DARK_GRAY}.\pNice to meet you!\pPeople always criticize me for being too friendly with others.\pBut I just find that's the best way to make new friends!\pWell, how about a battle, friend?")
}

text Zone4E_Text_Leviathan4Defeat {
	"That was a lovely battle.\n"
	"Thank you!"
}

script Zone4E_EventScript_ItemSuperPotion {
	lock
	if (flag(FLAG_ITEM_ZONE4E_SUPER_POTION)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE4E_SUPER_POTION)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_SUPER_POTION, 1)
	release
	end
}

raw `
Zone4E_EventScript_Sign::
	msgbox Zone4E_Text_Sign, MSGBOX_SIGN
	end

Zone4E_Text_Sign:
	.string "Trainer Tips!\p"
	.string "Golden Trees often have Berries\n"
	.string "or wild Pokémon!$"
`
