mapscripts Zone5A_MapScripts {}

script Zone5A_EventScript_Exit {
    lockall
    if (var(VAR_ZONE) > 5) {
		setvar(VAR_REVISITING_ZONE, 0)
		warpteleport(MAP_ZONE0D, 255, 31, 32)
		waitstate
    } else {
		if ((flag(FLAG_BRUTAL_MODE_ON) == true) &&
			(!defeated(TRAINER_ANNE_AND_JUNE) ||
			!defeated(TRAINER_GILBERT))) {
			goto(EventScript_BrutalExitNotSatisfied)
		}
        warp(MAP_ZONE5SANCTUARY, 255, 22, 7)
    }
    releaseall
    end
}

script Zone5A_EventScript_LeviathanBattle {
	lockall
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	if (var(VAR_TEMP_0) == 29) {
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
		waitmovement(0)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
		waitmovement(0)
	} elif (var(VAR_TEMP_0) == 27) {
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkRight)
		waitmovement(0)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
		waitmovement(0)
	}
	msgbox(format(
		"{COLOR BLUE}Kevin{COLOR DARK_GRAY}: So… if it isn't the trainer who's been beating all my fellow club members.\pWell then. Would you like to challenge the final member of the Fearsome Foursome?"
	), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		msgbox(format("{COLOR BLUE}Kevin{COLOR DARK_GRAY}: Oh, I see how it is. Scared, huh?"))
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
		waitmovement(0)
		releaseall
		end
	}
	msgbox(format(
		"{COLOR BLUE}Kevin{COLOR DARK_GRAY}: Finally! My Bugs are writhing with excitement!"
	))
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	trainerbattle_no_intro(TRAINER_KEVIN, format("It can't be!"))
	applymovement(10, Common_Movement_FaceRight)
	waitmovement(0)
	msgbox(format(
		"{COLOR BLUE}Kevin{COLOR DARK_GRAY}: Declan, man, if only you had joined our club!\pWe could have been the Fearsome Fivesome!"
	))
	setweather(WEATHER_RAIN)
	doweather
	msgbox(format("It suddenly began to rain!"))
	applymovement(4, Common_Movement_FaceLeft)
	waitmovement(0)
	msgbox(format(
		"{COLOR BLUE}Declan{COLOR DARK_GRAY}: …\n"
		"…\l"
		"I told you already, Kevin.\l"
		"I don't want to be part of that nonsense Fearsome Foursome club…\l"
		"…\l"
		"My club is the only club for me…"))
	fadeoutbgm(1)
	applymovement(4, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	msgbox(format("{COLOR BLUE}Declan{COLOR DARK_GRAY}: Ah! Another person! Hey you!"))
	applymovement(4, Common_Movement_WalkDown)
	waitmovement(0)
	applymovement(4, Common_Movement_FaceLeft)
	waitmovement(0)
	applymovement(10, Common_Movement_FaceDown)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
	waitmovement(0)
	msgbox(format(
		"{COLOR BLUE}Declan{COLOR DARK_GRAY}: Want to join my club? The name: The Bug Maniacs!\p"
		"…\n"
		"…\l"
		"You don't?!\p"
		"You'll change your mind after {COLOR BLUE}Leviathan Declan{COLOR DARK_GRAY} beats you with his fabulous Bug Pokémon!"))
	if (flag(FLAG_BRUTAL_MODE_ON) == false) {	
		special(HealPlayerParty)
	}
	setvar(VAR_MUGSHOT_ID, MUGSHOT_DECLAN)
	trainerbattle_no_intro(TRAINER_DECLAN_LEVIATHAN_5, Zone5A_Text_Leviathan5Defeat)
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	applymovement(10, Common_Movement_WalkRight)
	waitmovement(0)
	applymovement(10, Common_Movement_FaceDown)
	waitmovement(0)
	msgbox(format(
		"{COLOR BLUE}Kevin{COLOR DARK_GRAY}: Look, Declan, dude, I get it.\pYou don't want to join the Fearsome Fivesome.\pMaybe it doesn't have the right ring or something.\pBut at least let's train together. Maybe we can beat this trainer next time."
	))
	applymovement(4, Common_Movement_FaceUp)
	waitmovement(0)
	msgbox(format(
		"{COLOR BLUE}Declan{COLOR DARK_GRAY}: Fine. You're right, you're right."
	))
	delay(64)
	applymovement(4, Common_Movement_FaceLeft)
	waitmovement(0)
	msgbox(format(
		"{COLOR BLUE}Declan{COLOR DARK_GRAY}: Oh! Right. I forgot you're here.\p"
		"…\n"
		"…\l"
		"…\l"
		"A reward?\l"
		"…\l"
		"Look, I don't go around giving out sweet rewards to non-club members.\l"
		"…\l"
		"…\p"
		"All right, fine. Take this TM and leave me alone!"
	))
	giveitem(ITEM_TM07, 1)
	closemessage
	msgbox(format(
		"{COLOR BLUE}Kevin{COLOR DARK_GRAY}: Sheesh. Harsh, much?"
	))
	applymovement(4, Common_Movement_FaceUp)
	waitmovement(0)
	msgbox(format(
		"{COLOR BLUE}Declan{COLOR DARK_GRAY}: Oh, shut it. Let's just get out of here."
	))
	fadescreen(FADE_TO_BLACK)
	removeobject(4)
	setflag(FLAG_BEATLEVIATHAN5)
	removeobject(10)
	fadescreen(FADE_FROM_BLACK)
	setvar(VAR_ZONE_5_STATE, 1)
	release
	end
}

text Zone5A_Text_Leviathan5Defeat {
	format("But how?! My Bugs!")
}

raw `
Zone5A_EventScript_AnneAndJune::
	trainerbattle_double TRAINER_ANNE_AND_JUNE, Zone5A_Text_AnneAndJuneIntro, Zone5A_Text_AnneAndJuneDefeat, gText_NotEnoughMons
	msgbox Zone5A_Text_AnneAndJunePostBattle, MSGBOX_DEFAULT
	release
	end

Zone5A_Text_AnneAndJuneIntro:
	.string "Anne: We're teammates!\p"
	.string "June: No, I'm the Senior and\n"
	.string "you're the Junior!$"

Zone5A_Text_AnneAndJuneDefeat:
	.string "Anne: I guess you were right…\p"
	.string "June: We still lost though…$"

Zone5A_Text_AnneAndJunePostBattle:
	.string "Anne: Maybe we needed better…\p"
	.string "June: Coordination?\p"
	.string "Anne: Yeah, exactly that!$"

Zone5A_EventScript_Gilbert::
	trainerbattle_single TRAINER_GILBERT, Zone5A_Text_GilbertIntro, Zone5A_Text_GilbertDefeat
	msgbox Zone5A_Text_GilbertPostBattle, MSGBOX_AUTOCLOSE
	end

Zone5A_Text_GilbertIntro:
	.string "I've fished up a wild Trainer!$"

Zone5A_Text_GilbertDefeat:
	.string "You're too tough to take down!$"

Zone5A_Text_GilbertPostBattle:
	.string "I can't seem to catch a break!\p"
	.string "Get it? Catch?$"

Zone5A_EventScript_Rebecca::
	trainerbattle_single TRAINER_REBECCA, Zone5A_Text_RebeccaIntro, Zone5A_Text_RebeccaDefeat
	msgbox Zone5A_Text_RebeccaPostBattle, MSGBOX_AUTOCLOSE
	end

Zone5A_Text_RebeccaIntro:
	.string "In order to pass, you have to beat\n"
	.string "the Fearsome Foursome!$"

Zone5A_Text_RebeccaDefeat:
	.string "First of the Fearsome Foursome down!$"

Zone5A_Text_RebeccaPostBattle:
	.string "Defeat leaves me in disgrace…$"

Zone5A_EventScript_Caitlyn::
	trainerbattle_single TRAINER_CAITLYN, Zone5A_Text_CaitlynIntro, Zone5A_Text_CaitlynDefeat
	msgbox Zone5A_Text_CaitlynPostBattle, MSGBOX_AUTOCLOSE
	end

Zone5A_Text_CaitlynIntro:
	.string "The second of the Fearsome Foursome!\n"
	.string "That's me!$"

Zone5A_Text_CaitlynDefeat:
	.string "Second of the Fearsome Foursome down!$"

Zone5A_Text_CaitlynPostBattle:
	.string "They say second is the best…$"

Zone5A_EventScript_Ryan::
	trainerbattle_single TRAINER_RYAN, Zone5A_Text_RyanIntro, Zone5A_Text_RyanDefeat
	msgbox Zone5A_Text_RyanPostBattle, MSGBOX_AUTOCLOSE
	end

Zone5A_Text_RyanIntro:
	.string "Fearsome Foursome, number three!\n"
	.string "Be careful, I'm strong!$"

Zone5A_Text_RyanDefeat:
	.string "Third of the Fearsome Foursome down!$"

Zone5A_Text_RyanPostBattle:
	.string "There's only one left. Don't be afraid!$"

Zone5A_EventScript_ItemQuickBall::
	finditem ITEM_QUICK_BALL
	end

Zone5A_EventScript_ItemTM06::
	finditem ITEM_TM06
	end

Zone5A_EventScript_Sign::
	msgbox Zone5A_Text_Sign, MSGBOX_SIGN
	end

Zone5A_Text_Sign:
	.string "You can sort items in the Bag\n"
	.string "using {START_BUTTON}.$"
`
