mapscripts Zone19D_MapScripts {}

script Zone19D_EventScript_Sign {
	lock
	msgbox(format("This is just a normal wall!\pNothing suspicious at all behind it!"), MSGBOX_SIGN)
	if (var(VAR_ZONE_19_STATE) == 0) {
		goto(Zone19D_EventScript_InitiatePirateBattles)
	}
	release
	end
}

script Zone19D_EventScript_InitiatePirateBattles {
	fadeoutbgm(1)
	delay(40)
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	if (var(VAR_TEMP_0) == 10) {
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
		waitmovement(0)
	} else {
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
		waitmovement(0)
	}
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	playbgm(MUS_ENCOUNTER_AQUA, 0)
	addobject(1)
	addobject(2)
	addobject(3)
	addobject(4)
	clearflag(FLAG_HIDE_ZONE19D_PIRATES)
	applymovement(1, Zone19D_Movement_TopPirateWalkUp)
	applymovement(2, Zone19D_Movement_LeftPirateWalkUp)
	applymovement(3, Zone19D_Movement_BottomPirateWalkUp)
	applymovement(4, Zone19D_Movement_RightPirateWalkUp)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
	waitmovement(0)
	delay(16)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
	waitmovement(0)
	delay(16)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
	waitmovement(0)
	delay(40)
	applymovement(1, Common_Movement_WalkInPlaceFasterDown)
	msgbox("Shiver me timbers!\nAhoy, maties! This here trainer\lfound us!")
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
	waitmovement(0)
	applymovement(2, Common_Movement_WalkInPlaceFasterRight)
	msgbox("We'll be feedin' the fish if the\nCap'n ever finds out someone found\lour hideout.")
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
	waitmovement(0)
	applymovement(3, Common_Movement_WalkInPlaceFasterUp)
	msgbox("Well, dead men tell no tales.\pThe Cap'n will ne'er ‘ave to know,\nsavvy?")
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
	waitmovement(0)
	applymovement(4, Common_Movement_WalkInPlaceFasterLeft)
	msgbox("Ye lily-livered scallywags,\nlet's just get to the battles!")
	waitmovement(0)
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	trainerbattle_no_intro(TRAINER_PIRATE_1, Zone19D_Text_PirateDefeat)
	if ((flag(FLAG_SETTINGS_BRUTAL) == false) && (flag(FLAG_SETTINGS_CHALLENGE) == false)) {
		special(HealPlayerParty)
	}
	applymovement(3, Common_Movement_WalkInPlaceFasterUp)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
	waitmovement(0)
	msgbox("I be next. Fire in the hole!")
	trainerbattle_no_intro(TRAINER_PIRATE_2, Zone19D_Text_PirateDefeat)
	if ((flag(FLAG_SETTINGS_BRUTAL) == false) && (flag(FLAG_SETTINGS_CHALLENGE) == false)) {
		special(HealPlayerParty)
	}
	applymovement(2, Common_Movement_WalkInPlaceFasterRight)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
	waitmovement(0)
	msgbox("We give no quarter!")
	trainerbattle_no_intro(TRAINER_PIRATE_3, Zone19D_Text_PirateDefeat)
	if ((flag(FLAG_SETTINGS_BRUTAL) == false) && (flag(FLAG_SETTINGS_CHALLENGE) == false)) {
		special(HealPlayerParty)
	}
	applymovement(1, Common_Movement_WalkInPlaceFasterDown)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
	waitmovement(0)
	msgbox("Son o' a biscuit eater!\nFace the might o' us seadogs!")
	trainerbattle_no_intro(TRAINER_PIRATE_4, Zone19D_Text_PirateDefeat)
	if ((flag(FLAG_SETTINGS_BRUTAL) == false) && (flag(FLAG_SETTINGS_CHALLENGE) == false)) {
		special(HealPlayerParty)
	}
	msgbox("Sink me!\nThis trainer beat all four o' us!\l‘ere be your booty, take it!")
	giveitem(ITEM_TM36)
	msgbox("Me hearties, run!")
	applymovement(1, Zone19D_Movement_Pirate1Run)
	applymovement(2, Zone19D_Movement_Pirate2Run)
	applymovement(3, Zone19D_Movement_Pirate3Run)
	applymovement(4, Zone19D_Movement_Pirate4Run)
	msgbox("Arrr!")
	waitmovement(0)
	removeobject(1)
	removeobject(2)
	removeobject(3)
	removeobject(4)
	setflag(FLAG_HIDE_ZONE19D_PIRATES)
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	fadedefaultbgm
	setvar(VAR_ZONE_18_STATE, 5)
	setvar(VAR_ZONE_19_STATE, 1)
	setflag(FLAG_HIDE_ZONE18A_ZOE)
	setflag(FLAG_HIDE_ZONE18A_ELI)
	clearflag(FLAG_HIDE_ZONE18B_ZOE)
	clearflag(FLAG_HIDE_ZONE18B_ELI)
	clearflag(FLAG_HIDE_ZONE18A_PORYGON)
	delay(100)
	msgbox(format("It's time to go back to the Trainer's Dojo."))
	delay(40)
	warp(MAP_ZONE18B, 255, 4, 4)
	waitstate
	releaseall
	end
}

text Zone19D_Text_PirateDefeat {
	"Arrr!"
}

movement Zone19D_Movement_TopPirateWalkUp {
	walk_up * 5
	walk_right
	walk_up
	walk_right
	face_down
}

movement Zone19D_Movement_LeftPirateWalkUp {
	walk_up * 6
	walk_right * 2
}

movement Zone19D_Movement_BottomPirateWalkUp {
	walk_up * 5
}

movement Zone19D_Movement_RightPirateWalkUp {
	delay_16
	walk_up * 5
	walk_left
}

movement Zone19D_Movement_Pirate1Run {
	walk_faster_left
	walk_faster_down * 7
}

movement Zone19D_Movement_Pirate2Run {
	walk_faster_down
	walk_faster_left
	walk_faster_down * 5
}

movement Zone19D_Movement_Pirate3Run {
	walk_faster_down * 7
}

movement Zone19D_Movement_Pirate4Run {
	walk_faster_down * 2
	walk_faster_right
	walk_faster_down * 4
}