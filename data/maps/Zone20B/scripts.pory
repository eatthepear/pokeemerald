mapscripts Zone20B_MapScripts {
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_ZONE_20_STATE, 0: Zone20B_EventScript_LeviathanBattle
	]
}

script Zone20B_EventScript_LeviathanBattle {
    lockall
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}???{COLOR DARK_GRAY}: We've gone undetected thus far, but we need to hurry.\pThere's no telling when something…"))
	applymovement(1, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	applymovement(1, Common_Movement_FaceDown)
	waitmovement(0)
	fadeoutbgm(1)
	msgbox(format("{COLOR BLUE}???{COLOR DARK_GRAY}: How… did you find me?\pHow did he know to send you here?"), MSGBOX_DEFAULT)
	msgbox(format("{COLOR BLUE}???{COLOR DARK_GRAY}: No matter. I'll put you down and be out of here in a second."))
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	setvar(VAR_MUGSHOT_ID, MUGSHOT_MIRAGE)
	if ((flag(FLAG_SETTINGS_BRUTAL) == true) || (flag(FLAG_SETTINGS_CHALLENGE) == true)) {
		trainerbattle_no_intro(TRAINER_MIRAGE_LEVIATHAN_17_BRUTAL, Zone20B_Text_Leviathan17Defeat)
	} else {
		trainerbattle_no_intro(TRAINER_MIRAGE_LEVIATHAN_17, Zone20B_Text_Leviathan17Defeat)
	}
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	msgbox(format("{COLOR BLUE}???{COLOR DARK_GRAY}: That's… not possible.\pAn AI, defeating me?"), MSGBOX_DEFAULT)
	delay(100)
	applymovement(1, Common_Movement_FaceLeft)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}???{COLOR DARK_GRAY}: Command, I'm on my way out."))
	applymovement(1, Common_Movement_FaceUp)
	waitmovement(0)
	fadescreenswapbuffers(FADE_TO_BLACK)
	playmoncry(SPECIES_PORYGON, 2)
	delay(40)
	waitmoncry
	removeobject(1)
	removeobject(2)
	setflag(FLAG_BEAT_LEVIATHAN_17)
	fadescreenswapbuffers(FADE_FROM_BLACK)
	msgbox(format("There's something glittering on the ground!"), MSGBOX_DEFAULT)
	giveitem(ITEM_GREEN_SHARD, 1)
	setvar(VAR_ZONE_20_STATE, 1)
	releaseall
	end
}

text Zone20B_Text_Leviathan17Defeat {
	"…"
}
