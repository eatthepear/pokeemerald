mapscripts Zone11A_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE11A_TM22)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_TM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_TM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE11A_MOON_STONE)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE11A_TIMER_BALL)) {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone11A_EventScript_LeviathanIntro {
	lockall
	applymovement(6, Common_Movement_FaceLeft)
	waitmovement(0)
	applymovement(6, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	applymovement(6, Common_Movement_WalkLeft)
	waitmovement(0)
	applymovement(6, Common_Movement_WalkLeft)
	waitmovement(0)
	applymovement(6, Common_Movement_WalkLeft)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Newton{COLOR DARK_GRAY}: Excuse me, I realize this is rather sudden, but would you like to be a part of my experiment?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		msgbox(format("{COLOR BLUE}Newton{COLOR DARK_GRAY}: I see."))
		applymovement(6, Common_Movement_WalkRight)
		waitmovement(0)
		applymovement(6, Common_Movement_WalkRight)
		waitmovement(0)
		applymovement(6, Common_Movement_WalkRight)
		waitmovement(0)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
		waitmovement(0)
	} else {
		fadeoutbgm(1)
		msgbox(format("{COLOR BLUE}Newton{COLOR DARK_GRAY}: Thank you! The experiment name is: Gravity and its Pokémon battle-winning properties!"))
		setflag(FLAG_FORCE_BATTLE_ANIM_ON)
		setvar(VAR_MUGSHOT_ID, MUGSHOT_NEWTON)
		if ((flag(FLAG_SETTINGS_BRUTAL) == true) || (flag(FLAG_SETTINGS_CHALLENGE) == true)) {
			trainerbattle_no_intro(TRAINER_NEWTON_LEVIATHAN_10_BRUTAL, Zone11A_Text_Leviathan10Defeat)
		} else {
			trainerbattle_no_intro(TRAINER_NEWTON_LEVIATHAN_10, Zone11A_Text_Leviathan10Defeat)
		}
		applymovement(6, Common_Movement_FaceLeft)
		waitmovement(0)
		clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
		msgbox(format("{COLOR BLUE}Newton{COLOR DARK_GRAY}: Gravity is not the only way to utilize lower accuracy moves consistently.\pI'd like you to have this Wide Lens."), MSGBOX_DEFAULT)
		giveitem(ITEM_WIDE_LENS)
		msgbox(Zone11A_Text_Leviathan10Post)
		setflag(FLAG_BEAT_LEVIATHAN_10)
		setvar(VAR_ZONE_11_STATE, 1)
	}
	releaseall
	end
}

text Zone11A_Text_Leviathan10Defeat {
	"Alas, not even the power of Gravity is\nstrong enough to win me a battle…"
}

script Zone11A_EventScript_Leviathan {
	lock
	faceplayer
	msgbox(Zone11A_Text_Leviathan10Post)
	release
	end
}

text Zone11A_Text_Leviathan10Post {
	format("{COLOR BLUE}Newton{COLOR DARK_GRAY}: Unfortunately, I'm afraid the path ahead is blocked off until my experiments finish.\pPerhaps come back far in the future, when I've finished here.")
}

script Zone11A_EventScript_BuyRuin {
	lock
	faceplayer
	if (flag(FLAG_SETTINGS_NUZLOCKE) == true && flag(FLAG_ZONE11A_NUZLOCKE_BOUGHT_RUIN) == true) {
		msgbox(format("I'm not offering any of these other Pokémon for sale anymore."))
		release
		end
	} else {
		showmoneybox(0, 0, 0)
		msgbox(format("Hello, traveler! I search the world for ruins. It looks like there aren't any here, but I do have some rare Pokémon to offer you.\pFor ¥4000, you can have one of Baltoy, Golett, Yamask, or Yamask-G!"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			closemessage
			hidemoneybox
			msgbox(format("I promise it won't ruin you!"))
		} else {
			checkmoney(4000, 0)
			if (var(VAR_RESULT) == FALSE) {
				msgbox("You don't have enough money.")
				hidemoneybox
			} else {
				specialvar(VAR_TEMP_2, CalculatePlayerPartyCount)
				if (var(VAR_TEMP_2) == PARTY_SIZE) {
					msgbox(format("Your party is full. Clear a party slot, then come back."))
					hidemoneybox
					release
					end
				}
				call(EventScript_NuzlockeVerifyGift)
				removemoney(4000)
				updatemoneybox
				msgbox("Alrighty, I hope this won't ruin you!")
				closemessage
				hidemoneybox
				if (flag(FLAG_SETTINGS_NUZLOCKE) == true) {
					if (flag(FLAG_SETTINGS_RANDOMIZER) == true) {
						setvar(VAR_TEMP_1, SPECIES_BALTOY) // gets randomized anyways
					} else {
						setvar(VAR_TEMP_3, 0)
						checkspeciesclause(SPECIES_BALTOY)
						if (var(VAR_RESULT) == TRUE) {
							addvar(VAR_TEMP_3, 1)
						}
						checkspeciesclause(SPECIES_GOLETT)
						if (var(VAR_RESULT) == TRUE) {
							addvar(VAR_TEMP_3, 1)
						}
						checkspeciesclause(SPECIES_YAMASK)
						if (var(VAR_RESULT) == TRUE) {
							addvar(VAR_TEMP_3, 1)
						}
						checkspeciesclause(SPECIES_YAMASK_GALARIAN)
						if (var(VAR_RESULT) == TRUE) {
							addvar(VAR_TEMP_3, 1)
						}
						if (var(VAR_TEMP_3) == 4) {
							msgbox(format("Unfortunately, you have already obtained a Pokémon in each of these evolution lines."))
							addmoney(4000)
							release
							end
						}
						while (var(VAR_TEMP_1) == 0) {
							random(4)
							switch (var(VAR_RESULT)) {
								case 0:
									checkspeciesclause(SPECIES_BALTOY)
									if (var(VAR_RESULT) == TRUE) {
										continue
									}
									setvar(VAR_TEMP_1, SPECIES_BALTOY)
								case 1:
									checkspeciesclause(SPECIES_GOLETT)
									if (var(VAR_RESULT) == TRUE) {
										continue
									}
									setvar(VAR_TEMP_1, SPECIES_GOLETT)
								case 2:
									checkspeciesclause(SPECIES_YAMASK)
									if (var(VAR_RESULT) == TRUE) {
										continue
									}
									setvar(VAR_TEMP_1, SPECIES_YAMASK)
								case 3:
									checkspeciesclause(SPECIES_YAMASK_GALARIAN)
									if (var(VAR_RESULT) == TRUE) {
										continue
									}
									setvar(VAR_TEMP_1, SPECIES_YAMASK_GALARIAN)
							}
						}
					}
					setflag(FLAG_ZONE11A_NUZLOCKE_BOUGHT_RUIN)
				} else {
					msgbox(format("Choose which Pokémon you would like."), MSGBOX_DEFAULT)
					multichoice2(21, 0, Zone11A_Text_RuinMultichoice, TRUE)
					switch (var(VAR_RESULT)) {
						case 0:
							setvar(VAR_TEMP_1, SPECIES_BALTOY)
						case 1:
							setvar(VAR_TEMP_1, SPECIES_GOLETT)
						case 2:
							setvar(VAR_TEMP_1, SPECIES_YAMASK)
						case 3:
							setvar(VAR_TEMP_1, SPECIES_YAMASK_GALARIAN)
					}
				}
				givemon(VAR_TEMP_1, 20, ITEM_NONE)
				setmonmetlocation(VAR_TEMP_2, 246)
				bufferspeciesname(0, VAR_TEMP_1)
				msgbox("Here's your {STR_VAR_1}.")
				playfanfare(MUS_OBTAIN_ITEM)
				message(format("{PLAYER} received the {STR_VAR_1}!"))
				waitmessage
				waitfanfare
				msgbox(format("Would you like to give a nickname to {STR_VAR_1}?"), MSGBOX_YESNO)
				if (var(VAR_RESULT) == YES) {
					call(Common_EventScript_GetGiftMonPartySlot)
					call(Common_EventScript_NameReceivedPartyMon)
				}
				msgbox(format("I know these Pokémon are a little spooky, but they're quite endearing after a while!"), MSGBOX_DEFAULT)
			}
		}
	}
	release
	end
}

script Zone11A_EventScript_TM22 {
	lock
	if (flag(FLAG_ITEM_ZONE11A_TM22)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_TM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE11A_TM22)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_TM22, 1)
	release
	end
}

script Zone11A_EventScript_MoonStone {
	lock
	if (flag(FLAG_ITEM_ZONE11A_MOON_STONE)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE11A_MOON_STONE)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_MOON_STONE)
	release
	end
}

script Zone11A_EventScript_TimerBall {
	lock
	if (flag(FLAG_ITEM_ZONE11A_TIMER_BALL)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE11A_TIMER_BALL)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_TIMER_BALL, 1)
	release
	end
}

raw `
Zone11A_Text_RuinMultichoice:
    .string "Baltoy$"
    .string "Golett$"
    .string "Yamask$"
    .string "Yamask-G$"
    .string "$"

Zone11A_EventScript_CelebiIntro::
	lockall
	goto Zone11A_EventScript_NoCelebi
	releaseall
	end

Zone11A_EventScript_NoCelebi:
	msgbox Zone11A_Text_SacredTree, MSGBOX_AUTOCLOSE
	releaseall
	end

Zone11A_Text_SacredTree:
	.string "This appears to be a sacred tree.$"

Zone11A_EventScript_Jeanette::
	trainerbattle_single TRAINER_JEANETTE, Zone11A_Text_JeanetteIntro, Zone11A_Text_JeanetteDefeat
	msgbox Zone11A_Text_JeanettePostBattle, MSGBOX_AUTOCLOSE
	end

Zone11A_Text_JeanetteIntro:
	.string "Ah! An angry spirit!$"

Zone11A_Text_JeanetteDefeat:
	.string "I wasn't talking about you,\n"
	.string "I meant behind you!$"

Zone11A_Text_JeanettePostBattle:
	.string "It's gone now…$"

Zone11A_EventScript_EmmaandLeo::
	trainerbattle_double TRAINER_EMMA_AND_LEO, Zone11A_Text_EmmaandLeoIntro, Zone11A_Text_EmmaandLeoDefeat, gText_NotEnoughMons
	msgbox Zone11A_Text_EmmaandLeoPostBattle, MSGBOX_DEFAULT
	release
	end

Zone11A_Text_EmmaandLeoIntro:
	.string "Leo: I'm gonna show my girlfriend\n"
	.string "that I'm brave!\p"
	.string "Emma: Ugh. Enforcing gender norms,\n"
	.string "am I right?$"

Zone11A_Text_EmmaandLeoDefeat:
	.string "Leo: My toxic masculinity, ruined!\p"
	.string "Emma: Maybe I need a new SO…$"

Zone11A_Text_EmmaandLeoPostBattle:
	.string "Leo: Don't talk to my girlfriend!\p"
	.string "Emma: Pff.$"

Zone11A_EventScript_Joel::
	trainerbattle_single TRAINER_JOEL, Zone11A_Text_JoelIntro, Zone11A_Text_JoelDefeat
	msgbox Zone11A_Text_JoelPostBattle, MSGBOX_AUTOCLOSE
	end

Zone11A_Text_JoelIntro:
	.string "Ugh, another forest maze?\n"
	.string "At least this one doesn't have warps…$"

Zone11A_Text_JoelDefeat:
	.string "Another defeat?\n"
	.string "Luck is just not on my side today…$"

Zone11A_Text_JoelPostBattle:
	.string "I'll see you another day.$"

Zone11A_EventScript_Marion::
	trainerbattle_single TRAINER_MARION, Zone11A_Text_MarionIntro, Zone11A_Text_MarionDefeat
	msgbox Zone11A_Text_MarionPostBattle, MSGBOX_AUTOCLOSE
	end

Zone11A_Text_MarionIntro:
	.string "My psychic powers have no match!$"

Zone11A_Text_MarionDefeat:
	.string "You're too powerful…$"

Zone11A_Text_MarionPostBattle:
	.string "Your power level…\n"
	.string "It's over 9000!$"

Zone11A_EventScript_Regina::
	trainerbattle_single TRAINER_REGINA, Zone11A_Text_ReginaIntro, Zone11A_Text_ReginaDefeat
	msgbox Zone11A_Text_ReginaPostBattle, MSGBOX_AUTOCLOSE
	end

Zone11A_Text_ReginaIntro:
	.string "Do you hear the call?$"

Zone11A_Text_ReginaDefeat:
	.string "The spirits… I can hear their voices!$"

Zone11A_Text_ReginaPostBattle:
	.string "It might just be the wind whistling,\n"
	.string "but I'm sure the voices are from the\l"
	.string "beyond.$"
`
