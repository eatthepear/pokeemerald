mapscripts Zone7B_MapScripts {}

script Zone7B_EventScript_LeviathanBattle {
	lock
	faceplayer
	if (flag(FLAG_BEAT_LEVIATHAN_7) == true) {
		msgbox(Zone7B_Text_LeviathanPost)
		release
		end
	}
	msgbox(Zone7B_Text_Leviathan7WantBattle, MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		msgbox(format("{COLOR BLUE}Betty{COLOR DARK_GRAY}: Well, I will be here, if you change your mind."))
		release
		end
	}
	fadeoutbgm(1)
	msgbox(format("{COLOR BLUE}Betty{COLOR DARK_GRAY}: {COLOR BLUE}Leviathan Betty{COLOR DARK_GRAY} will show you how to behave in the prim and proper way!"), MSGBOX_DEFAULT)
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	if ((flag(FLAG_SETTINGS_BRUTAL) == true) || (flag(FLAG_SETTINGS_CHALLENGE) == true)) {
    	setvar(VAR_MUGSHOT_ID, MUGSHOT_BETTY)
		trainerbattle_no_intro(TRAINER_BETTY_LEVIATHAN_7_BRUTAL, Zone7B_Text_Leviathan7Defeat)
	} else {
		setvar(VAR_MUGSHOT_ID, MUGSHOT_BETTY)
		trainerbattle_no_intro(TRAINER_BETTY_LEVIATHAN_7, Zone7B_Text_Leviathan7Defeat)
	}
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	msgbox(format("{COLOR BLUE}Betty{COLOR DARK_GRAY}: Your battling skills are certainly up to par. Excellent form.\pAside from that, I'd like to give you these Berries.\pVery, very expensive, or so my butler tells me."), MSGBOX_DEFAULT)
	giveitem(ITEM_SITRUS_BERRY, 5)
	msgbox(Zone7B_Text_LeviathanPost)
	closemessage
	setflag(FLAG_BEAT_LEVIATHAN_7)
	release
	end
}

text Zone7B_Text_Leviathan7WantBattle {
	format("{COLOR BLUE}Betty{COLOR DARK_GRAY}: Your clothes are all covered in dirt.\pI think you could use a lesson in proper etiquette, what do you say?")
}

text Zone7B_Text_Leviathan7Defeat {
	"It seems I was the one who ended\n"
	"up being humbled…"
}

text Zone7B_Text_LeviathanPost {
	format("{COLOR BLUE}Betty{COLOR DARK_GRAY}: I've been staying here at my friend's house while our new mansion gets renovated.\pIsn't that exciting?")
}

raw `

Zone7B_EventScript_Lady::
	lock
	faceplayer
	goto_if_set FLAG_SETTINGS_BRUTAL, Zone7B_EventScript_AlreadyHealed
	goto_if_set FLAG_SETTINGS_INDEPENDENT, Zone7B_EventScript_AlreadyHealed
	goto_if_set FLAG_ZONE7B_HEAL_USED, Zone7B_EventScript_AlreadyHealed
	msgbox Zone7B_Text_WantToHeal, MSGBOX_YESNO
	compare VAR_RESULT, YES
	goto_if_eq Zone7B_EventScript_Heal
	msgbox Zone7B_Text_ComeBack, MSGBOX_AUTOCLOSE
	release
	end

Zone7B_EventScript_AlreadyHealed:
	msgbox Zone7B_Text_GoodLuck, MSGBOX_AUTOCLOSE
	release
	end

Zone7B_EventScript_Heal:
	msgbox Zone7B_Text_RestWell, MSGBOX_AUTOCLOSE
	fadescreen FADE_TO_BLACK
	playfanfare MUS_HEAL
	waitfanfare
	special HealPlayerParty
	fadescreen FADE_FROM_BLACK
	setflag FLAG_ZONE7B_HEAL_USED
	goto Zone7B_EventScript_AlreadyHealed
	end

Zone7B_Text_WantToHeal:
	.string "Greetings, child.\n"
	.string "To ease your journey, you may rest\l"
	.string "at my humble home.\p"
	.string "I only have enough medicine to\n"
	.string "heal your Pokémon once though.\p"
	.string "Would you like to rest, child?$"

Zone7B_Text_RestWell:
	.string "Rest well, child.$"

Zone7B_Text_GoodLuck:
	.string "Best of luck on your journey, child.$"

Zone7B_Text_ComeBack:
	.string "Come back when you need to rest, child.$"
`
