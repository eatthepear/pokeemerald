mapscripts Zone16A_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE16A_TM27)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE16A_WATER_STONE)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE16A_DIVE_BALL)) {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE16A_TIMER_BALL)) {
			setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone16A_EventScript_Michael {
	lock
	if (var(VAR_ZONE_16_STATE) != 0) {
		faceplayer
		msgbox(format("{COLOR BLUE}Michael{COLOR DARK_GRAY}: You're amazingingly skilled!"))
		release
		end
	}
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	if (var(VAR_TEMP_0) == 66) {
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
		waitmovement(0)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkRight)
		waitmovement(0)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
		waitmovement(0)
	}
	goto(Zone16A_EventScript_InitiateKatieMichaelBattles)
}

script Zone16A_EventScript_Katie {
	lock
	if (var(VAR_ZONE_16_STATE) != 0) {
		faceplayer
		msgbox(format("{COLOR BLUE}Katie{COLOR DARK_GRAY}: Treat that Lapras right!"))
		release
		end
	}
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	if (var(VAR_TEMP_0) == 69) {
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
		waitmovement(0)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
		waitmovement(0)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
		waitmovement(0)
	}
	goto(Zone16A_EventScript_InitiateKatieMichaelBattles)
}

script Zone16A_EventScript_InitiateKatieMichaelBattles {
	msgbox(format("{COLOR BLUE}Michael{COLOR DARK_GRAY}: Hello! We're the dynamic swimming duo!"))
	msgbox(format("{COLOR BLUE}Katie{COLOR DARK_GRAY}: Listen up, 'cause we've got a challenge for you!"))
	msgbox(format("{COLOR BLUE}Michael{COLOR DARK_GRAY}: Our friend is a Pokémon Ranger and she's looking for someone to take care of a rehabilitated Lapras."))
	msgbox(format("{COLOR BLUE}Katie{COLOR DARK_GRAY}: But she wants to make sure the person is a skilled trainer!"))
	if (flag(FLAG_BRUTAL_MODE_ON) == true) {
		msgbox(format("{COLOR BLUE}Michael{COLOR DARK_GRAY}: If you beat us both back-to-back in battle, I'll let her know to give the Lapras to you! You won't be healed in between battles."), MSGBOX_YESNO)
	} else {
		msgbox(format("{COLOR BLUE}Michael{COLOR DARK_GRAY}: If you beat us both back-to-back in battle, I'll let her know to give the Lapras to you! You will be healed in between battles."), MSGBOX_YESNO)
	}
	if (var(VAR_RESULT) == NO) {
		msgbox(format("{COLOR BLUE}Katie{COLOR DARK_GRAY}: I guess Lapras may never find the right home…"))
		release
		end
	}
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	msgbox(format("{COLOR BLUE}Katie{COLOR DARK_GRAY}: Alright! Let's get started, then!"))
	trainerbattle_no_intro(TRAINER_KATIE, format("Katie: You made quite the splash!"))
	if (flag(FLAG_BRUTAL_MODE_ON) == false) {
		special(HealPlayerParty)
	}
	msgbox(format("{COLOR BLUE}Michael{COLOR DARK_GRAY}: I hope you're ready for this battle!"))
	trainerbattle_no_intro(TRAINER_MICHAEL, format("Michael: You're too quick!"))
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	msgbox(format("{COLOR BLUE}Katie{COLOR DARK_GRAY}: You got us! You're an amazing trainer!\pWe'll put in the good word with our friend."))
	msgbox(format("{COLOR BLUE}Michael{COLOR DARK_GRAY}: I think she's somewhere in the shallows to the south of us right now."))
	msgbox(format("{COLOR BLUE}Katie{COLOR DARK_GRAY}: Treat that Lapras right!"))
	setvar(VAR_ZONE_16_STATE, 1)
	release
	end
}

script Zone16A_EventScript_PokemonRanger {
	lock
	faceplayer
	if (var(VAR_ZONE_16_STATE) == 0) {
		msgbox(format("You look like a skilled trainer!\pI have here a Lapras that's been rehabilitated from a boating collision.\pIt's not fit to live in the wild anymore, so I've been looking for skilled trainers to take care of it.\pIf you want to show me your skills, go find {COLOR BLUE}Katie{COLOR DARK_GRAY} and {COLOR BLUE}Michael{COLOR DARK_GRAY} to the north of here."))
		release
		end
	}
	if (var(VAR_ZONE_16_STATE) == 1) {
		msgbox(format("You look like a skilled trainer!\pI have here a Lapras that's been rehabilitated from a boating collision.\pI heard from {COLOR BLUE}Katie{COLOR DARK_GRAY} and {COLOR BLUE}Michael{COLOR DARK_GRAY} that you're a skilled trainer.\pWould you be willing to take care of Lapras?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			msgbox(format("I'm worried that no one will be able to take care of this rare Pokémon.\pIf you change your mind, let me know."))
			release
			end
		}
		specialvar(VAR_TEMP_0, CalculatePlayerPartyCount)
		if (var(VAR_TEMP_0) == PARTY_SIZE) {
			msgbox(format("It looks like your party is full. Come back with space in your party, yeah?"))
			release
			end
		}
		givemon(SPECIES_LAPRAS, 30, ITEM_NONE)
		setmonmetlocation(VAR_TEMP_0, 249)
		setvar(VAR_TEMP_1, SPECIES_LAPRAS)
		playfanfare(MUS_OBTAIN_ITEM)
		message("Received Lapras!")
		waitmessage
		waitfanfare
		bufferspeciesname(0, SPECIES_LAPRAS)
		msgbox(format("Would you like to give a nickname to Lapras?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			call(Common_EventScript_GetGiftMonPartySlot)
			call(Common_EventScript_NameReceivedPartyMon)
		}
		setvar(VAR_ZONE_16_STATE, 2)
	}
	msgbox(format("Thank you, trainer, for taking care of Lapras."), MSGBOX_DEFAULT)
	release
	end
}

script Zone16A_EventScript_Exit {
    lockall
    if (var(VAR_ZONE) > 16) {
		call(EventScript_WarpZone0)
    } else {
		specialvar(VAR_NUM_TRAINERS_REMAINING, GetNumTrainersRemaining)
		if ((flag(FLAG_BRUTAL_MODE_ON) == true) && (var(VAR_NUM_TRAINERS_REMAINING) != 0)) {
			goto(EventScript_BrutalExitNotSatisfied)
		}
        warp(MAP_ZONE16SANCTUARY, 255, 22, 7)
		waitstate
    }
    releaseall
    end
}

script Zone16A_EventScript_SandPile {
	lockall
	random(5)
	if (var(VAR_RESULT) == 0) {
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
		msgbox(format("There was a Pokémon in the sand pile!"), MSGBOX_DEFAULT)
		waitmovement(0)
		closemessage
		random(4)
		switch (var(VAR_RESULT)) {
			case 0:
				setwildbattle(SPECIES_PINCURCHIN, 28, ITEM_NONE)
				playmoncry(SPECIES_PINCURCHIN, 2)
				delay(40)
				waitmoncry
			case 1:
				setwildbattle(SPECIES_PYUKUMUKU, 28, ITEM_NONE)
				playmoncry(SPECIES_PYUKUMUKU, 2)
				delay(40)
				waitmoncry
			case 2:
				setwildbattle(SPECIES_SANDYGAST, 28, ITEM_NONE)
				playmoncry(SPECIES_SANDYGAST, 2)
				delay(40)
				waitmoncry
			case 3:
				setwildbattle(SPECIES_WIMPOD, 28, ITEM_NONE)
				playmoncry(SPECIES_WIMPOD, 2)
				delay(40)
				waitmoncry
		}
		dowildbattle
	} else {
		msgbox("It's a pile of sand.")
	}
	releaseall
	end
}

script Zone16A_EventScript_TM27 {
	lock
	if (flag(FLAG_ITEM_ZONE16A_TM27)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE16A_TM27)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_TM27, 1)
	release
	end
}

script Zone16A_EventScript_WaterStone {
	lock
	if (flag(FLAG_ITEM_ZONE16A_WATER_STONE)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE16A_WATER_STONE)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_WATER_STONE, 1)
	release
	end
}

script Zone16A_EventScript_DiveBall {
	lock
	if (flag(FLAG_ITEM_ZONE16A_DIVE_BALL)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE16A_DIVE_BALL)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_DIVE_BALL, 1)
	release
	end
}

script Zone16A_EventScript_TimerBall {
	lock
	if (flag(FLAG_ITEM_ZONE16A_TIMER_BALL)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE16A_TIMER_BALL)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_TIMER_BALL, 1)
	release
	end
}

raw `

Zone16A_EventScript_Sign::
	msgbox Zone16A_Text_Sign, MSGBOX_SIGN
	end

Zone16A_Text_Sign:
	.string "Sand piles may contain Pokémon.\p"
	.string "Be cautious!\n"
	.string "They don't like being disturbed!$"

Zone16A_EventScript_Iona::
	trainerbattle_single TRAINER_IONA, Zone16A_Text_IonaIntro, Zone16A_Text_IonaDefeat
	msgbox Zone16A_Text_IonaPostBattle, MSGBOX_AUTOCLOSE
	end

Zone16A_Text_IonaIntro:
	.string "The Water Pokémon are tickling\n"
	.string "my toes!$"

Zone16A_Text_IonaDefeat:
	.string "Oh, that was just seaweed.$"

Zone16A_Text_IonaPostBattle:
	.string "Don't you love that there is such\n"
	.string "a diversity of Water Pokémon?$"

Zone16A_EventScript_Berke::
	trainerbattle_single TRAINER_BERKE, Zone16A_Text_BerkeIntro, Zone16A_Text_BerkeDefeat
	msgbox Zone16A_Text_BerkePostBattle, MSGBOX_AUTOCLOSE
	end

Zone16A_Text_BerkeIntro:
	.string "Just keep swimming,\n"
	.string "just keep swimming!$"

Zone16A_Text_BerkeDefeat:
	.string "It's a good motto, right?$"

Zone16A_Text_BerkePostBattle:
	.string "Who's Dory?$"

Zone16A_EventScript_Tanisha::
	trainerbattle_single TRAINER_TANISHA, Zone16A_Text_TanishaIntro, Zone16A_Text_TanishaDefeat
	msgbox Zone16A_Text_TanishaPostBattle, MSGBOX_AUTOCLOSE
	end

Zone16A_Text_TanishaIntro:
	.string "Have you ever been to a\n"
	.string "Lazy River?$"

Zone16A_Text_TanishaDefeat:
	.string "It's the best place for Tubers!$"

Zone16A_Text_TanishaPostBattle:
	.string "Lazy River is the best!$"

Zone16A_EventScript_Robert::
	trainerbattle_single TRAINER_ROBERT, Zone16A_Text_RobertIntro, Zone16A_Text_RobertDefeat
	msgbox Zone16A_Text_RobertPostBattle, MSGBOX_AUTOCLOSE
	end

Zone16A_Text_RobertIntro:
	.string "Have you ever been to a\n"
	.string "Lazy River?$"

Zone16A_Text_RobertDefeat:
	.string "It's the best place for Tubers!$"

Zone16A_Text_RobertPostBattle:
	.string "Lazy River is the best!$"

Zone16A_EventScript_Chris::
	trainerbattle_single TRAINER_CHRIS, Zone16A_Text_ChrisIntro, Zone16A_Text_ChrisDefeat
	msgbox Zone16A_Text_ChrisPostBattle, MSGBOX_AUTOCLOSE
	end

Zone16A_Text_ChrisIntro:
	.string "Have you ever been to a\n"
	.string "Lazy River?$"

Zone16A_Text_ChrisDefeat:
	.string "It's the best place for Tubers!$"

Zone16A_Text_ChrisPostBattle:
	.string "Lazy River is the best!$"

Zone16A_EventScript_Angus::
	trainerbattle_single TRAINER_ANGUS, Zone16A_Text_AngusIntro, Zone16A_Text_AngusDefeat
	msgbox Zone16A_Text_AngusPostBattle, MSGBOX_AUTOCLOSE
	end

Zone16A_Text_AngusIntro:
	.string "Fishing isn't just a hobby.\n"
	.string "It's a lifestyle!$"

Zone16A_Text_AngusDefeat:
	.string "I've fished up defeat!$"

Zone16A_Text_AngusPostBattle:
	.string "Dive Balls can help capture\n"
	.string "Pokémon while fishing.$"

`
